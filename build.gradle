plugins {
    id 'java'
    id "org.springframework.boot" version "2.2.4.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

project.group    = "${artifactGroup}"
project.version  = "${artifactVersion}"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-redis-reactive"
    implementation "org.springframework.session:spring-session-data-redis"
    implementation "com.querydsl:querydsl-jpa"
    implementation "org.mindrot:jbcrypt:${jbcrypt}"

    implementation "com.google.guava:guava:${guava}"
    compileOnly "org.projectlombok:lombok:${lombok}"
    testCompileOnly "org.projectlombok:lombok:${lombok}"
    annotationProcessor "org.projectlombok:lombok:${lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombok}"

    runtimeOnly 'com.h2database:h2'

    testImplementation "org.springframework.boot:spring-boot-starter-test"
}


// querydsl 적용
def querydslSrcDir = 'src/main/generated'

querydsl {
    library = "com.querydsl:querydsl-apt"
    jpa = true
    querydslSourcesDir = querydslSrcDir
}

compileQuerydsl{
    options.annotationProcessorPath = configurations.querydsl
}

configurations {
    querydsl.extendsFrom compileClasspath
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', querydslSrcDir]
        }
    }
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors()
    useJUnitPlatform()
}